# Grupo: Hackintosh Brasil
# Autor: Jonas Wander
#
# Compat√≠vel com MaciASL (fork Rehabman)
# https://bitbucket.org/RehabMan/os-x-maciasl-patchmatic/downloads/
# 
# Lenovo G40-80 

# METHOD FOR 16 BITS - CREDITS REHABMAN
into method label B1B2 remove_entry;
into definitionblock code_regex . insert
begin
Method (B1B2, 2, NotSerialized) { Return(Or(Arg0, ShiftLeft(Arg1, 8))) }\n
end;

# METHODS FOR MORE THAN 32 BITS - CREDITS REHABMAN
into method label RE1B parent_label EC0 remove_entry;
into method label RECB parent_label EC0 remove_entry;
into device label EC0 insert
begin
Method (RE1B, 1, NotSerialized)\n
{\n
    OperationRegion(ERAM, EmbeddedControl, Arg0, 1)\n
    Field(ERAM, ByteAcc, NoLock, Preserve) { BYTE, 8 }\n
    Return(BYTE)\n
}\n
Method (RECB, 2, Serialized)\n
// Arg0 - offset in bytes from zero-based EC\n
// Arg1 - size of buffer in bits\n
{\n
    ShiftRight(Add(Arg1,7), 3, Arg1)\n
    Name(TEMP, Buffer(Arg1) { })\n
    Add(Arg0, Arg1, Arg1)\n
    Store(0, Local0)\n
    While (LLess(Arg0, Arg1))\n
    {\n
        Store(RE1B(Arg0), Index(TEMP, Local0))\n
        Increment(Arg0)\n
        Increment(Local0)\n
    }\n
    Return(TEMP)\n
}\n
end;

# FIX 16 BITS

into device label EC0 code_regex B1RC,\s+16 replace_matched begin RC00,8,RC01,8 end;
into device label EC0 code_regex B1FV,\s+16 replace_matched begin FV00,8,FV01,8 end;
into device label EC0 code_regex B1DV,\s+16 replace_matched begin DV00,8,DV01,8 end;
into device label EC0 code_regex B1DC,\s+16 replace_matched begin DC00,8,DC01,8 end;
into device label EC0 code_regex B1FC,\s+16 replace_matched begin FC00,8,FC01,8 end;
into device label EC0 code_regex B1AC,\s+16 replace_matched begin AC00,8,AC01,8 end;

into method label SMTF code_regex \(B1RC, replaceall_matched begin (B1B2(RC00,RC01), end;
into method label SMTF code_regex \(B1FV, replaceall_matched begin (B1B2(FV00,FV01), end;
into method label SMTF code_regex \(B1FC, replaceall_matched begin (B1B2(FC00,FC01), end;
into method label SMTF code_regex \(B1AC, replaceall_matched begin (B1B2(AC00,AC01), end;

into method label _BST code_regex \(B1RC, replaceall_matched begin (B1B2(RC00,RC01), end;
into method label _BST code_regex \(B1FV, replaceall_matched begin (B1B2(FV00,FV01), end;
into method label _BST code_regex \(B1AC, replaceall_matched begin (B1B2(AC00,AC01), end;

into method label _BIF code_regex \(B1DV, replaceall_matched begin (B1B2(DV00,DV01), end;
into method label _BIF code_regex \(B1DC, replaceall_matched begin (B1B2(DC00,DC01), end;
into method label _BIF code_regex \(B1FC replaceall_matched begin (B1B2(FC00,FC01) end;

# FIX MORE THAN 32 BITS

into device label EC0 code_regex (FWBT,)\s+(64) replace_matched begin FWBZ,%2,//%1%2 end;
into device label EC0 code_regex (SMDA,)\s+(256) replace_matched begin SMDZ,%2,//%1%2 end;

into method label MHIF code_regex \(FWBT, replaceall_matched begin (RECB(0x14,64), end;
into method label MHPF code_regex SMDA replaceall_matched begin RECB(0x64,256) end;

into method label GBID code_regex \(FWBT, replaceall_matched begin (RECB(0x14,64), end;
